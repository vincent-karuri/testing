buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'org.apache.commons:commons-lang3:3.3.2'
        classpath 'org.robolectric:robolectric-gradle-plugin:1.1.0'
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
    }
}

allprojects {
    repositories {
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url "https://maven.google.com" }
        mavenLocal()
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://jitpack.io' }

}

android {
    compileSdkVersion 22
    buildToolsVersion "26.0.1"

    defaultConfig {
        applicationId "org.smartregister.path"
        minSdkVersion 18
        targetSdkVersion 21
        versionCode 14
        versionName "1.4.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    dexOptions {
        incremental true
        javaMaxHeapSize "4g"
    }

    lintOptions {
        lintConfig file("lint.xml")
    }

    buildTypes {
        release {
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", 'opensrp_url', '""'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_INITIAL_BATCH_SIZE", '250'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_BATCH_SIZE", '100'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_SOURCE", '1'
            buildConfigField "int", "DATABASE_VERSION", '12'
            buildConfigField "long", "MAX_SERVER_TIME_DIFFERENCE", "1800000l"
            buildConfigField "boolean", "TIME_CHECK", "true"
            buildConfigField "int", "AUTO_SYNC_DURATION", '15'
            buildConfigField "int", "WEIGHT_SYNC_PROCESSING_MINUTES", '30'
            buildConfigField "int", "VACCINE_SYNC_PROCESSING_MINUTES", '30'
            buildConfigField "int", "RECURRING_SERVICES_SYNC_PROCESSING_MINUTES", '30'
            buildConfigField "int", "DAILY_TALLIES_GENERATION_MINUTES", '60'
            buildConfigField "int", "COVERAGE_DROPOUT_GENERATION_MINUTES", '60'
            buildConfigField "int", "IMAGE_UPLOAD_MINUTES", '180'
            buildConfigField "int", "PULL_UNIQUE_IDS_MINUTES", '180'
        }

        debug {
            resValue "string", 'opensrp_url', '"https://zeir-stage.smartregister.org/opensrp"'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_INITIAL_BATCH_SIZE", '250'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_BATCH_SIZE", '100'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_SOURCE", '2'
            buildConfigField "int", "DATABASE_VERSION", '12'
            buildConfigField "long", "MAX_SERVER_TIME_DIFFERENCE", "1800000l"
            buildConfigField "boolean", "TIME_CHECK", "false"
            buildConfigField "int", "AUTO_SYNC_DURATION", '2'
            buildConfigField "int", "WEIGHT_SYNC_PROCESSING_MINUTES", '3'
            buildConfigField "int", "VACCINE_SYNC_PROCESSING_MINUTES", '3'
            buildConfigField "int", "RECURRING_SERVICES_SYNC_PROCESSING_MINUTES", '3'
            buildConfigField "int", "DAILY_TALLIES_GENERATION_MINUTES", '5'
            buildConfigField "int", "COVERAGE_DROPOUT_GENERATION_MINUTES", '5'
            buildConfigField "int", "IMAGE_UPLOAD_MINUTES", '7'
            buildConfigField "int", "PULL_UNIQUE_IDS_MINUTES", '10'
            testCoverageEnabled true
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'LICENSE.txt'
    }


    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    compile fileTree(include: ['commons-validator-1.6.jar'], dir: 'libs')
    compile('org.smartregister:opensrp-client-native-form:1.0.3@aar') {
        transitive = true
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    compile('org.smartregister:opensrp-client-core:1.2.3@aar') {
        transitive = true
        exclude group: 'com.github.bmelnychuk', module: 'atv'
        exclude group: 'org.ei.drishti', module: 'drishti-interface'
    }
    compile('org.smartregister:opensrp-client-immunization:1.2.6@aar') {
        transitive = true
        exclude group: 'org.smartregister', module: 'opensrp-client-core'
        exclude group: 'org.smartregister', module: 'opensrp-client-native-form'
    }
    compile('org.smartregister:opensrp-client-growth-monitoring:1.0.6@aar') {
        transitive = true
        exclude group: 'org.smartregister', module: 'opensrp-client-core'
    }

    compile('org.smartregister:opensrp-client-stock:1.0.1@aar') {
        transitive = true
        exclude group: 'org.smartregister', module: 'opensrp-client-core'
        exclude group: 'org.smartregister', module: 'opensrp-client-native-form'
    }

    compile('com.android.support:design:22.2.1') {
        exclude group: 'com.android.support', module: 'recyclerview-v7'

    }
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:cardview-v7:22.2.1'
    compile 'com.android.support:leanback-v17:22.2.1'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }
    compile 'com.jjoe64:graphview:4.2.1'
    compile 'com.android.support:support-v4:22.2.1'
    compile 'id.zelory:compressor:1.0.4'
    compile 'com.twotoasters.SectionCursorAdapter:library:1.0.+'
    compile 'com.github.ybq:Android-SpinKit:1.1.0'
    compile 'junit:junit:4.12'
    compile 'org.pcollections:pcollections:2.1.3'

    testCompile('org.robolectric:robolectric:3.4.2') {

        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'org.apache.maven', module: 'maven-model'
        exclude group: 'org.apache.maven', module: 'maven-ant-tasks'
        exclude group: 'com.android.support'
    }

    compile group: 'net.vidageek', name: 'mirror', version: '1.6.1'

    testCompile("org.robolectric:shadows-multidex:3.4-rc2") {
        exclude group: 'com.google.guava', module: 'guava'
    }

    // Android runner and rules support
    androidTestCompile('com.android.support.test:runner:0.5') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile ('com.android.support.test:rules:0.5') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    // Espresso support
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // add this for intent mocking support
    androidTestCompile('com.android.support.test.espresso:espresso-intents:2.2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    // PowerMock
    def powerMockVersion = '1.7.1'
    testCompile "org.powermock:powermock-module-junit4:$powerMockVersion"
    testCompile "org.powermock:powermock-module-junit4-rule:$powerMockVersion"
    testCompile "org.powermock:powermock-api-mockito2:$powerMockVersion"
    testCompile("org.powermock:powermock-classloading-xstream:$powerMockVersion") {

        exclude group: 'com.thoughtworks.xstream', module: 'xstream'
    }

    testCompile 'org.apache.maven:maven-ant-tasks:2.1.3'
}



//Testing and coveralls

coveralls {
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/merged.xml"
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebug']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    getReports().getXml().setDestination(file("${buildDir}/reports/jacoco/jacocoRootReport/merged.xml"))
    getReports().getHtml().setDestination(file("${buildDir}/reports/jacoco/jacocoRootReport/html"))

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', '**/*$ViewBinder*.*']
    def debugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebug.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ])
}

//End testing and coveralls